<strategy> ::= "All Operators" <defaultOperation>

<defaultOperation> ::= <operatorOperation> <defaultOperation> | <mutantOperation> <defaultOperation> | "New Branch" <defaultOperation> <defaultOperation> | "Store Mutants"

<selectionType> ::= "Sequential" | "Random" | "LastToFirst"

<sortingDirection> ::= "Ascending" | "Descending"

<quantity> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "10"

<percentage> ::= "0.1" | "0.2" | "0.3" | "0.4" | "0.5" | "0.6" | "0.7" | "0.8" | "0.9" | "1.0"

## Operações de Operadores

<operatorOperation> ::= "Retain Operators" <selectOperators> | "Discard Operators" <selectOperators> | "Execute Operators" <operatorExecutionType> <selectOperators>

<selectOperators> ::= <operatorSelectionType> <quantity> | <operatorSelectionType> <percentage>

<operatorSelectionType> ::= "Select Operators" <selectionType> <operatorSorting> | "Select Operators by Groups" <operatorGroupSelectionType> <selectOperators>

<operatorSorting> ::=  <operatorAttribute> <sortingDirection> | ""

<operatorGroupSelectionType> ::= <operatorGrouping> <selectionType> <operatorGroupSorting>

<operatorGrouping> ::= <operatorAttribute>

<operatorGroupSorting> ::=  <operatorGroupAttribute> <sortingDirection> | ""

<operatorAttribute> ::= "Type" | "Mutant Quantity"

<operatorGroupAttribute> ::= "Quantity in Group" | "Mutant Quantity in Group"

<operatorExecutionType> ::= "Conventional"

## Operações de Mutantes

<mutantOperation> ::= "Retain Mutants" <selectMutants> | "Discard Mutants" <selectMutants>

<selectMutants> ::= <mutantSelectionType> <quantity> | <mutantSelectionType> <percentage>

<mutantSelectionType> ::= "Select Mutants" <selectionType> <mutantSorting> | "Select Mutants by Groups" <mutantGroupSelectionType> <selectMutants>

<mutantSorting> ::= <mutantAttribute> <sortingDirection> | ""

<mutantGroupSelectionType> ::= <mutantGrouping> <selectionType> <mutantGroupSorting>

<mutantGrouping> ::= <mutantAttribute>

<mutantGroupSorting> ::= <mutantGroupAttribute> <sortingDirection> | ""

<mutantAttribute> ::= "Operator Type" | "Operator"

<mutantGroupAttribute> ::= "Quantity in Group"